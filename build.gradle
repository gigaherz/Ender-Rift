// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

repositories {
    maven {
        url "http://maven.tterrag.com/"
    }
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        url 'http://maven.epoxide.xyz'
    }
    maven {
        url 'http://dogforce-games.com/maven'
    }
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "2.1.9"
group= "gigaherz.enderRift"
archivesBaseName = "EnderRift-1.12.1"

minecraft {
    version = "1.12.1-14.22.0.2445"
    runDir = "run"

    mappings = "snapshot_20170804"

    replace "@VERSION@", project.version
    replaceIn "EnderRiftMod.java"
}

configurations {
    // configuration that holds jars to embed inside the jar
    embed
}

dependencies {
    deobfCompile "gigaherz.commons:gigaherz.commons-1.12.1:0.6.4"
    embed "gigaherz.commons:gigaherz.commons-1.12.1:0.6.4"

    deobfCompile "gigaherz.graph:GraphLib:1.3.1"
    embed "gigaherz.graph:GraphLib:1.3.0"

    deobfCompile "gigaherz.guidebook:Guidebook-1.12.0:1.7.3"

    deobfCompile "mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.15-8"
    deobfCompile "net.darkhax.tesla:Tesla-1.12:1.0.61"

    deobfCompile "mezz.jei:jei_1.12:4.7.4.80:api"
    runtime "mezz.jei:jei_1.12:4.7.4.80"

    compile fileTree(dir: 'libs', include: '*.jar')
}

jar {
    into('/') {
        from configurations.embed
    }

    manifest {
        attributes("ContainedDeps": configurations.embed.collect { it.getName() }.join(' '))
    }
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
