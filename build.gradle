// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

repositories {
    maven {
        url "https://maven.tterrag.com/"
    }
    maven {
        url "https://dvs1.progwml6.com/files/maven"
    }
    maven {
        url 'https://dogforce-games.com/maven'
    }
    maven {
        url 'https://maven.mcmoddev.com/'
    }
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
    maven {url "http://tehnut.info/maven"}
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "2.1.10"
group= "gigaherz.enderRift"
archivesBaseName = "EnderRift-1.12.2"

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "stable_39"

    replace "@VERSION@", project.version
    replaceIn "EnderRiftMod.java"
}

configurations {
    // configuration that holds jars to embed inside the jar
    embed
    embed.transitive = false
}

dependencies {
    deobfCompile "gigaherz.commons:gigaherz.commons-1.12.1:0.6.4"
    embed "gigaherz.commons:gigaherz.commons-1.12.1:0.6.4"

    deobfCompile "gigaherz.graph:GraphLib2:2.0.0"
    embed "gigaherz.graph:GraphLib2:2.0.0"

    deobfCompile "net.darkhax.gamestages:GameStages-1.12.2:2.0.113"
    deobfCompile "net.darkhax.bookshelf:Bookshelf-1.12.2:2.3.584"

    deobfCompile "gigaherz.guidebook:Guidebook-1.12.2:2.9.0"

    deobfCompile "mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.28-17"

    deobfCompile "net.darkhax.tesla:Tesla-1.12.2:1.0.63"

    deobfCompile "mezz.jei:jei_1.12.2:4.15.0.278:api"
    runtime "mezz.jei:jei_1.12.2:4.15.0.278"

    deobfCompile "mcp.mobius.waila:Hwyla:1.8.26-B41_1.12.2"

    compile fileTree(dir: 'libs', include: '*.jar')
}

jar {
    into('/') {
        from configurations.embed
    }

    manifest {
        attributes("ContainedDeps": configurations.embed.collect { it.getName() }.join(' '))
    }
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
