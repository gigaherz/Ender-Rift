// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

repositories {
    maven {
        url "http://maven.tterrag.com/"
    }
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        url 'http://maven.epoxide.xyz'
    }
    maven {
        url 'http://dogforce-games.com/maven'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "2.1.4"
group= "gigaherz.enderRift"
archivesBaseName = "EnderRift-1.11.2"

minecraft {
    version = "1.11.2-13.20.0.2201"
    runDir = "run"

    mappings = "snapshot_20161210"

    replace "@VERSION@", project.version
    replaceIn "EnderRiftMod.java"
}

jar {
    classifier = 'slim'
}

shadowJar {
    classifier = ''
    dependencies {
        include(dependency(':GraphLib:'))
        include(dependency(':Commons:'))
    }
    relocate 'gigaherz.common', 'gigaherz.enderRift.shadow.common'
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

tasks.build.dependsOn reobfShadowJar

artifacts {
    archives shadowJar
    archives sourceJar
}

dependencies {
    deobfCompile "gigaherz.commons:Commons:1.11.0-0.4.5"
    deobfCompile "gigaherz.graph:GraphLib:1.3.0"
    deobfCompile "gigaherz.guidebook:Guidebook-1.11.0:1.3.0"

    deobfCompile "mcjty.theoneprobe:TheOneProbe:1.11-1.3.4-47"
    deobfCompile "net.darkhax.tesla:Tesla:1.11-1.3.0.51"
    deobfCompile "mezz.jei:jei_1.11:4.0.5.203:api"
    runtime "mezz.jei:jei_1.11:4.0.5.203"
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
